import groovy.xml.MarkupBuilder
import org.jooq.util.GenerationTool

import javax.xml.bind.JAXB

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'org.flywaydb:flyway-gradle-plugin:3.2.1'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'
        classpath 'org.jooq:jooq-codegen:3.7.2'
        classpath 'org.postgresql:postgresql:9.4.1207'
    }
}

plugins {
    id 'application'
    id 'idea'
    id 'java'
    id 'jetty'
    id 'org.flywaydb.flyway' version '3.2.1'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id "com.github.ben-manes.versions" version '0.12.0'
    id 'org.unbroken-dome.test-sets' version '1.2.0'
}

group 'todoer'
version '1.0-SNAPSHOT'

String getDbUrl() {
    String databaseUrl = System.getenv("JDBC_DATABASE_URL") ?: System.getenv("JDBC_TODO_DEV_URL")
    if (databaseUrl == null) {
        throw new RuntimeException("No matching environmental variables for "
                        + "JDBC_DATABASE_URL or JDBC_TODO_DEV_URL");
    }
    return databaseUrl;
}

project.ext {
    configPath = "$rootProject.projectDir/"
    configFile = configPath + 'todo-config.yml'
    dbUrl = getDbUrl()
}

project.sourceCompatibility = 1.8
project.targetCompatibility = 1.8

sourceSets {
        main.java.srcDir 'src/generated/java'
}

//noinspection GroovyUnusedAssignment
mainClassName = "todoer.TodoApp"

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

testSets {
    integrationTest
}

dependencies {
    compile (
            'com.google.guava:guava:19.0',
            'com.google.code.gson:gson:2.5',
            'com.fasterxml.jackson.core:jackson-core:2.7.0',
            'com.fasterxml.jackson.core:jackson-databind:2.7.0',
            'com.sparkjava:spark-core:2.3',
            'org.apache.commons:commons-dbcp2:2.1.1',
            'org.apache.httpcomponents:httpcore:4.4.4',
            'org.flywaydb:flyway-core:3.2.1',
            'org.projectlombok:lombok:1.16.6',
            'javax.persistence:persistence-api:1.0.2',
            'javax.validation:validation-api:1.1.0.Final',
            'javax.ws.rs:javax.ws.rs-api:2.0.1',
            "org.jooq:jooq:3.7.2",
            'org.slf4j:slf4j-simple:1.7.14'
    )

    testCompile (
            'junit:junit:4.12'
    )

    integrationTestCompile (
            'info.cukes:cucumber-java:1.2.4',
            'info.cukes:cucumber-junit:1.2.4'
    )

    runtime (
            'org.postgresql:postgresql:9.4.1207'
    )
}

flyway {
     url = dbUrl
}

// https://github.com/flyway/flyway/issues/775
// Flyway needs to support Java migrations, so it depends on testClasses.
// That results in a circular dependency for jOOQ because we need migrations
// to run, then jOOQ code-gen and then compileJava
project.afterEvaluate {
    flywayClean.dependsOn -= testClasses
    flywayMigrate.dependsOn = [processResources, processTestResources]
}


task generateJooqCode {
//    description = 'Generate Java API for the database.'
    doLast {
        // https://github.com/jOOQ/jOOQ/tree/master/jOOQ-examples/jOOQ-codegen-gradle
        def writer = new StringWriter()
        def xml = new MarkupBuilder(writer)
        xml.configuration('xmlns': 'http://www.jooq.org/xsd/jooq-codegen-3.7.0.xsd') {
            jdbc() {
                driver('org.postgresql.Driver')
                url(dbUrl)
            }
            generator() {
                database() {
                    inputSchema('todo_backend')
                }

                // Watch out for this caveat when using MarkupBuilder with "reserved names"
                // - https://github.com/jOOQ/jOOQ/issues/4797
                // - http://stackoverflow.com/a/11389034/521799
                // - https://groups.google.com/forum/#!topic/jooq-user/wi4S9rRxk4A
                generate([:]) {
                    pojos false
                    immutablePojos false
                    daos false
                    fluentSetters true
                    validationAnnotations true
                    jpaAnnotations true
                    globalObjectReferences true
                }
                target() {
                    packageName('todoer.models')
                    directory("src/generated/java")
                }
            }
        }

        // Run the code generator
        GenerationTool.generate(
                JAXB.unmarshal(new StringReader(writer.toString()),
                        org.jooq.util.jaxb.Configuration.class)
        )
    }
}

generateJooqCode.dependsOn flywayMigrate
generateJooqCode.dependsOn = [processResources, processTestResources]

compileJava.dependsOn generateJooqCode

check.dependsOn integrationTest
integrationTest.mustRunAfter test

// Always run integration tests
project.integrationTest {
    outputs.upToDateWhen { false }
}

// Ensure each type of test gets a separate directory
tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

// Don't run tests on stage, because integration tests will wipe
// the database.
task stage {
    dependsOn shadowJar
}